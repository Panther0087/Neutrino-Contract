
{-# STDLIB_VERSION 3 #-}
{-# CONTENT_TYPE DAPP #-}
{-# SCRIPT_TYPE ACCOUNT #-}

# base function
func getNumberByKey(key: String) = {
    match getInteger(this, key) {
            case a:Int => a
            case _ => 0
    }
}
func getStringByKey(key: String) = {
    match getString(this, key) {
            case a:String => a
            case _ => ""
    }
}
func getBoolByKey(key: String) = {
    match getBoolean(this, key) {
            case a:Boolean => a
            case _ => false
    }
}
func getNumberByAddressAndKey(address: String, key: String) = {
    match getInteger(addressFromStringValue(address), key) {
            case a:Int => a
            case _ => 0
    }
}
func getStringByAddressAndKey(address: String, key: String) = {
    match getString(addressFromStringValue(address), key) {
            case a:String => a
            case _ => ""
    }
}
#end base function

# consts
let LISTSPLITSYMBOL = "_"
let PERCENTACCURACY = 10000
# end consts

#constructor 
let NeutrinoAssetIdKey = "neutrino_asset_id"
let BondAssetIdKey = "bond_asset_id"
let NeutrinoContractKey = "neutrino_contract"
#end constructor
 
# keys
let BalanceKey = "rpd_balance"
let SyncIndexKey = "rpd_sync_index"
let ProfitKey = "rpd_profit"
let BalanceHistoryKey = "balance_history"
let IsClaimedKey = "is_claimed"

func getUserBalanceHistoryKey(owner: String) = BalanceHistoryKey + "_" + owner
func getUserBalanceKey(owner: String, assetId: String) = BalanceKey + "_" + assetId + "_" + owner
func getUserBalanceKeyByCount(owner: String, count: Int, assetId: String) = getUserBalanceKey(owner, assetId) + "_" + toString(count)
func getIsClaimedKey(owner: String, count: Int) = IsClaimedKey + "_" + owner + "_" + toString(count)

func getContractBalanceKey(assetId: String) = BalanceKey + "_" + assetId
func getSnapshotContractBalanceKey(count: Int, assetId: String) = BalanceKey + "_" + assetId + "_" + toString(count)
func getProfitKey(count: Int) = ProfitKey + "_" + toString(count)
#end keys

#global vars
let neutrinoContract = getStringByKey(NeutrinoContractKey)
let neutrinoAssetId = fromBase58String(getStringByAddressAndKey(neutrinoContract, NeutrinoAssetIdKey))
let bondAssetId = fromBase58String(getStringByAddressAndKey(neutrinoContract, BondAssetIdKey))
let syncIndex = getNumberByAddressAndKey(neutrinoContract, SyncIndexKey)
#end global vars

# get func
func getContractBalance(assetId: String) = getNumberByKey(getContractBalanceKey(assetId))
func getSnapshotContractBalance(count: Int, assetId: String) = getNumberByAddressAndKey(neutrinoContract, getSnapshotContractBalanceKey(count, assetId))
func getProfit(count: Int) = getNumberByAddressAndKey(neutrinoContract, getProfitKey(count))

func getUserBalance(owner : String, assetId: String) = getNumberByKey(getUserBalanceKey(owner, assetId))
func getUserBalanceByCount(owner : String, count: Int, assetId: String) = getNumberByKey(getUserBalanceKeyByCount(owner, count, assetId))
func getUserBalanceHistory(owner: String) = getStringByKey(getUserBalanceHistoryKey(owner))
func isClaimed(owner: String, count: Int) = getBoolByKey(getIsClaimedKey(owner, count))
#end get func

func getHistoryElement(count: Int) = {
     toString(count) + LISTSPLITSYMBOL
}
func addBalanceHistory(history: String, element: String) = {
    history + element
}

@Callable(i)
func lockNeutrino() = {
    let pmt = extract(i.payment)
    if (pmt.assetId != neutrinoAssetId && pmt.assetId != bondAssetId) 
        then throw("can use neutrino or bond only")
    else {
        let account = toString(i.caller)
        let assetIdString = toBase58String(value(pmt.assetId))
        let balance = getUserBalance(account, assetIdString) + pmt.amount
        let historyString = getUserBalanceHistory(account)
        let history = historyString.split(LISTSPLITSYMBOL)
        let index = size(history) - 2;
        let newHistory = if(historyString != "" && parseIntValue(history[index]) == syncIndex) 
                        then historyString 
                    else (addBalanceHistory(historyString, getHistoryElement(syncIndex)))
        WriteSet([
            DataEntry(getContractBalanceKey(assetIdString), getContractBalance(assetIdString) + pmt.amount),
            DataEntry(getUserBalanceKey(account, assetIdString), balance),
            DataEntry(getUserBalanceKeyByCount(account, syncIndex, assetIdString), balance),
            DataEntry(getUserBalanceHistoryKey(account), newHistory)
        ])
    }
}

@Callable(i)
func unlockNeutrino(unlockAmount: Int, assetIdString: String) = {
    let account = toString(i.caller)
    let assetId = fromBase58String(assetIdString)
    let balance = getUserBalance(account, assetIdString) - unlockAmount
    if (balance < 0) 
        then throw("invalid amount")
    else if(assetId != neutrinoAssetId && assetId != bondAssetId)
        then throw("can use neutrino or bond only")
    else {
        let historyString = getUserBalanceHistory(account)
        let history = historyString.split(LISTSPLITSYMBOL)
        let index = size(history) - 2;
        let newHistory = if(historyString != "" && parseIntValue(history[index]) == syncIndex) 
                        then historyString 
                     else (addBalanceHistory(historyString, getHistoryElement(syncIndex)))
        ScriptResult(
            WriteSet([
                DataEntry(BalanceKey, getContractBalance(assetIdString) - unlockAmount),
                DataEntry(getUserBalanceKey(account, assetIdString), balance),
                DataEntry(getUserBalanceKeyByCount(account, syncIndex, assetIdString), balance),
                DataEntry(getUserBalanceHistoryKey(account), newHistory)
            ]),
            TransferSet([ScriptTransfer(addressFromStringValue(account), unlockAmount, neutrinoAssetId)])
        )
    }
}

@Callable(i)
func withdrawRPD(profitSyncIndex: Int, historyIndex: Int) = {
    let account = toString(i.caller)
    let historyString = getUserBalanceHistory(account)
    let history = historyString.split(LISTSPLITSYMBOL)
    let lockActual = parseIntValue(history[historyIndex])

    if(isClaimed(account, profitSyncIndex)) 
        then throw("account is claimed rpd_count:" + toString(profitSyncIndex))
    else if(profitSyncIndex > lockActual || (historyIndex + 2 < size(history) && profitSyncIndex < parseIntValue(history[historyIndex+1]) ))
        then throw("invalid history index")
    else {
        let neutrinoAssetIdString = toBase58String(neutrinoAssetId)
        let bondAssetIdString = toBase58String(bondAssetId)
        let balance = getUserBalanceByCount(account, lockActual, neutrinoAssetIdString) + getUserBalanceByCount(account, lockActual, bondAssetIdString)
        let sharePercent = balance*PERCENTACCURACY*100/(getSnapshotContractBalance(profitSyncIndex, neutrinoAssetIdString) + getSnapshotContractBalance(profitSyncIndex, bondAssetIdString))
        let profit = sharePercent*getProfit(profitSyncIndex)/100/PERCENTACCURACY
        
        ScriptResult(
            WriteSet([DataEntry(getIsClaimedKey(account, profitSyncIndex), true)]),
            TransferSet([ScriptTransfer(addressFromStringValue(account), profit, neutrinoAssetId)])
        )
    }
}